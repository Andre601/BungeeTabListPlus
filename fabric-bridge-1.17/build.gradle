plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

ext {
    minecraft_version = "1.17"
    yarn_mappings = "1.17+build.13"
    loader_version = "0.11.6"
    fabric_version = "0.36.0+1.17"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "btlp-fabric-bridge-1.17"

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://nexus.codecrafter47.de/content/repositories/public/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.ext.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.ext.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.ext.loader_version}"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.ext.fabric_version}"

    implementation "de.codecrafter47.data:api:${rootProject.ext.dataApiVersion}"
    implementation "de.codecrafter47.data.bukkit:api:${rootProject.ext.dataApiVersion}"
    implementation "de.codecrafter47.data:bukkit:${rootProject.ext.dataApiVersion}"
    implementation "de.codecrafter47.data:minecraft:${rootProject.ext.dataApiVersion}"
    implementation project(":bungeetablistplus-common")
    implementation project(":bungeetablistplus-bridge")

    shadow "de.codecrafter47.data:api:${rootProject.ext.dataApiVersion}"
    shadow "de.codecrafter47.data.bukkit:api:${rootProject.ext.dataApiVersion}"
    shadow "de.codecrafter47.data:bukkit:${rootProject.ext.dataApiVersion}"
    shadow "de.codecrafter47.data:minecraft:${rootProject.ext.dataApiVersion}"
    shadow project(":bungeetablistplus-common")
    shadow project(":bungeetablistplus-bridge")

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'
}

tasks.withType(JavaCompile).configureEach {

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
}

shadowJar {
    configurations = [project.configurations.shadow]
    dependsOn jar
    finalizedBy remapJar
    exclude '**/Log4j2Plugins.dat'
    archiveClassifier.set("shadow")
}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile.get()
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}
